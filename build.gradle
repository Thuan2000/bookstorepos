import java.sql.DriverManager

plugins {
    id 'java'
    id 'application'
    id 'org.javamodularity.moduleplugin' version '1.8.12'
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id 'org.beryx.jlink' version '2.25.0'
}

group 'com.thuannguyen'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

ext {
    junitVersion = '5.9.2'
}

sourceCompatibility = '17'
targetCompatibility = '17'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

application {
    mainModule = 'com.thuannguyen.bookstorepos'
    mainClass = 'com.thuannguyen.bookstorepos.Main'
}

javafx {
    version = '17.0.6'
    modules = ['javafx.controls', 'javafx.fxml', 'javafx.web']
}

dependencies {
    implementation('org.controlsfx:controlsfx:11.1.2')
    implementation('com.dlsc.formsfx:formsfx-core:11.6.0') {
        exclude(group: 'org.openjfx')
    }
    implementation('org.kordamp.ikonli:ikonli-javafx:12.3.1')
    implementation('org.kordamp.bootstrapfx:bootstrapfx-core:0.4.0')
    implementation('io.github.palexdev:materialfx:11.16.1')

    // MySQL JDBC Driver
    implementation ('mysql:mysql-connector-java:8.0.23')

    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
}

test {
    useJUnitPlatform()
}

jlink {
    imageZip = project.file("${buildDir}/distributions/app-${javafx.platform.classifier}.zip")
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'app'
    }
}

jlinkZip {
    group = 'distribution'
}

task setupDatabase() {
    doLast {
        // Set up the class loader with the runtime classpath
        URLClassLoader classLoader = GroovyObject.class.classLoader
        project.configurations.runtimeClasspath.each { classLoader.addURL(it.toURI().toURL()) }

        // Load JDBC driver
        Class.forName('com.mysql.cj.jdbc.Driver', true, classLoader)

        // Configuration
        def url = 'jdbc:mysql://localhost:3306/bookstore'
        def user = 'thuan'
        def password = '123!@#'

        // Connect to the database
        def connection = DriverManager.getConnection(url, user, password)

        // Create tables
        def createBookTableScript = new File('src/main/resources/db/migration/create-book.sql')
        def createOrderTableScript = new File('src/main/resources/db/migration/create-order.sql')
        def createOrderDetailsTableScript = new File('src/main/resources/db/migration/create-order-details.sql')
        connection.createStatement().execute(createBookTableScript.text)
        connection.createStatement().execute(createOrderTableScript.text)
        connection.createStatement().execute(createOrderDetailsTableScript.text)

        // Insert fake seed data
        def insertBooksScript = new File('src/main/resources/db/seed/insert-books.sql')
        def insertOrdersScript = new File('src/main/resources/db/seed/insert-orders.sql')
        def insertOrderDetailsScript = new File('src/main/resources/db/seed/insert-order-details.sql')
        connection.createStatement().execute(insertBooksScript.text)
        connection.createStatement().execute(insertOrdersScript.text)
        connection.createStatement().execute(insertOrderDetailsScript.text)

        // Close the connection
        connection.close()
    }
}